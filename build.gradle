plugins {
    id 'java'
    id 'distribution'
    id 'edu.sc.seis.launch4j' version '2.5.4'
}

version = '0.2.0-SNAPSHOT'

sourceCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'cz.minecart.updater.UpdaterApp'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
}

launch4j {
    outfile = "minecart-updater.exe"
    icon = "../../images/icon.ico"
    mainClassName = project.mainClass
}

jar {
    from { configurations.runtimeClasspath.findAll { it.name == 'json-simple-1.1.1.jar' }.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes(
            'Main-Class': mainClass
        )
    }
}

distributions {
    main {
        contents {
            
        }
    }
}
